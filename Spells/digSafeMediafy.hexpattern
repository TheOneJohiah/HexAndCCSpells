//Raycast block
//Check block against blacklist
//Dig block
//Zone item scan
//Foreach item, 
Consideration: 
{
    Mind's Reflection
    Compass' Purification
    Mind's Reflection
    Alidade's Purification
    Archer's Distillation //Raycast block
    Gemini Decomposition //Duplicate the coordinate
    Sorter's Purification //Get block type at coord
    List: "Nondig blocks"
    Jester's Gambit
    Single's Purification //Put type in list
    Intersection Distillation
    Abacus Purification //Get the length of the intersection
    Numerical Reflection: 0
    Equality Distillation //If no overlap, True, otherwise, False

    Consideration: 
    {
        Break Block
    }
    Consideration: 
    {
        Bookkeeper's Gambit: v //Clear the unused block coord
    }
    Augur's Exaltation //Dig if true, skip if false
    Hermes' Gambit

    //Mediafy time
    Consideration: 
    {
        Gemini Decomposition //Duplicate the entity ref
        Sorter's Purification //Get the itemtype
        Stocktake Purification //Get mote list like the itemtype
        Gemini Decomposition //Duplicate the new list
        Abacus Purification //Get the length of the list
        Numerical Reflection: 0
        Maximus Distillation //If list greater than 0, True, otherwise, False

        Consideration: 
        {
            Numerical Reflection: 0
            Selection Distillation //Pull first mote type out of mote list
            Mediafy Item //Add item to mote
        }
        Consideration: 
        {
            Bookkeeper's Gambit: vv
        }
        Augur's Exaltation //Mediafy if in motes already, skip if false
        Hermes' Gambit
    }
    Mind's Reflection
    Compass' Purification
    Numerical Reflection: 30
    Zone Distillation: Item
    Thoth's Gambit
}